dnl Process this file with autoconf to produce a configure script.
dnl This version of log4cplus requires the follwing autotools versions:
dnl autoconf-2.62
dnl automake-1.10.1
dnl libtool-2.2.6
AC_INIT([log4cplus],[1.1.0])
AC_CANONICAL_TARGET
AC_CONFIG_SRCDIR([src/logger.cxx])
AC_CONFIG_MACRO_DIR([m4])
AM_INIT_AUTOMAKE([1.10.1 no-define nostdinc gnu -Wall])

#
# +1 : ? : +1  == new interface that does not break old one
# +1 : ? : 0   == new interfac that breaks old one
#  ? : ? : 0   == no new interfaces, but breaks apps
#  ? :+1 : ?   == just some internal changes, nothing breaks but might work
#                 better
# CURRENT : REVISION : AGE
LT_VERSION=0:0:0
AC_SUBST([LT_VERSION])

dnl Sane locale?

AH_TEMPLATE([LOG4CPLUS_WORKING_LOCALE],
  [Define for compilers/standard libraries that support more than
   just the "C" locale.])

AC_ARG_WITH([working-locale],
  [AS_HELP_STRING([--with-working-locale],
    [Use std::locale for char->wchar_t conversion.])],
  [with_working_locale=yes],
  [with_working_locale=no])

AS_IF([test "x$with_working_locale" = "xyes"],
  [AC_DEFINE([LOG4CPLUS_WORKING_LOCALE], [1])],
  [])

dnl Debugging or release build?

AC_ARG_ENABLE([debugging],
  [AS_HELP_STRING([--enable-debugging],
    [Turns off optimization and adds switches that generate debugging
    information.])],
  [enable_debugging=yes],
  [enable_debugging=no])

AH_TEMPLATE([LOG4CPLUS_DEBUGGING],
  [Defined for --enable-debugging builds.])

AS_IF([test "x$enable_debugging" = "xyes"],
  [AC_DEFINE([LOG4CPLUS_DEBUGGING])
   LOG4CPLUS_NDEBUG=-UNDEBUG],
  [LOG4CPLUS_NDEBUG=-DNDEBUG])
AC_SUBST([LOG4CPLUS_NDEBUG])

dnl Check for c/c++ compilers.

AC_PROG_CXX
AC_PROG_CXXCPP
AC_LANG([C++])

AC_ARG_ENABLE([profiling],
  [AS_HELP_STRING([--enable-profiling],
    [Compile with profiling compiler options.])],
  [enable_profiling=yes],
  [enable_profiling=no])
LOG4CPLUS_PROFILING_LDFLAGS=
LOG4CPLUS_PROFILING_CXXFLAGS=

dnl Libtool setup.

LT_PREREQ([2.2.6])
LT_INIT

dnl Checks for programs.
AC_SEARCH_LIBS([strerror], [cposix])

AX_CXXFLAGS_WARN_ALL
AC_CXX_COMPILER_VENDOR([ac_cxx_compiler_vendor])
AS_CASE([$ac_cxx_compiler_vendor],
  [gnu],
  [AX_CXXFLAGS_GCC_OPTION([-Wextra])
  AX_CXXFLAGS_GCC_OPTION([-ansi])
  AX_CXXFLAGS_GCC_OPTION([-pedantic])
  AX_CXXFLAGS_GCC_OPTION([-Wstrict-aliasing])
  AX_CXXFLAGS_GCC_OPTION([-Wstrict-overflow])
dnl  AX_CXXFLAGS_GCC_OPTION([-Weffc++])
  AX_CXXFLAGS_GCC_OPTION([-Woverloaded-virtual])
  AX_CXXFLAGS_GCC_OPTION([-Wold-style-cast])
dnl  AX_CXXFLAGS_GCC_OPTION([-Wabi])
  AX_CXXFLAGS_GCC_OPTION([-Wc++0x-compat])
dnl  AX_CXXFLAGS_GCC_OPTION([-Wconversion])
  AX_CXXFLAGS_GCC_OPTION([-Wundef])
  AX_CXXFLAGS_GCC_OPTION([-Wshadow])

  AS_IF([test "x$enable_debugging" = "xyes"],
    [AX_CXXFLAGS_GCC_OPTION([-g3])
     AX_CXXFLAGS_GCC_OPTION([-O0])],
    [AX_CXXFLAGS_GCC_OPTION([-O2])])

  dnl Try -pg profiling option.
  AS_IF([test "x$enable_profiling" = "xyes"],
    [AX_CXXFLAGS_GCC_OPTION([-pg],[],
      [LOG4CPLUS_PROFILING_LDFLAGS="-pg"
       LOG4CPLUS_PROFILING_CXXFLAGS="-pg"])
     AX_CXXFLAGS_GCC_OPTION([-g3])])

  dnl The --enable-auto-import and --enable-runtime-pseudo-reloc
  dnl options are required for log4cplus to work when compiled with
  dnl Cygwin's GCC 4.3.x.
  AS_CASE(["$target_os"],
    [cygwin|mingw*],
    [LDFLAGS="${LDFLAGS} -Wl,--enable-auto-import"
     LDFLAGS="${LDFLAGS} -Wl,--enable-runtime-pseudo-reloc"])])

AX_DECLSPEC([LOG4CPLUS_DECLSPEC])
AS_IF([test "x$ac_cv_declspec" = "xyes" || test "x$ac_cv__attribute__visibility" = "xyes"],
  [AS_CASE([$ac_cxx_compiler_vendor],
    [gnu],
    [AS_IF([test "x$ac_cv_declspec" = "xyes" || test "x$ac_cv__attribute__visibility" = "xyes"],
       [AX_CXXFLAGS_GCC_OPTION([-fvisibility=hidden])])
     AX_CXXFLAGS_GCC_OPTION([-fkeep-inline-functions])])])

AX_C99_VARIADIC_MACROS
AH_TEMPLATE([LOG4CPLUS_HAVE_C99_VARIADIC_MACROS])
AS_IF([test "x$ac_cv_c99_variadic_macros" = "xyes"],
  [AC_DEFINE([LOG4CPLUS_HAVE_C99_VARIADIC_MACROS])])

AX_GNU_VARIADIC_MACROS
AH_TEMPLATE([LOG4CPLUS_HAVE_GNU_VARIADIC_MACROS])
AS_IF([test "x$ac_cv_gnu_variadic_macros" = "xyes"],
  [AC_DEFINE([LOG4CPLUS_HAVE_GNU_VARIADIC_MACROS])])

AX___FUNCTION___MACRO
AH_TEMPLATE([LOG4CPLUS_HAVE_FUNCTION_MACRO])
AS_IF([test "x$ac_cv_have___function___macro" = "xyes"],
  [AC_DEFINE([LOG4CPLUS_HAVE_FUNCTION_MACRO])])

AX___PRETTY_FUNCTION___MACRO
AH_TEMPLATE([LOG4CPLUS_HAVE_PRETTY_FUNCTION_MACRO])
AS_IF([test "x$ac_cv_have___pretty_function___macro" = "xyes"],
  [AC_DEFINE([LOG4CPLUS_HAVE_PRETTY_FUNCTION_MACRO])])

dnl Checks for libraries.

AC_CHECK_LIB([rt], [nanosleep])
AC_CHECK_FUNC([gethostent], [], [AC_CHECK_LIB([nsl], [gethostent])])
AC_CHECK_FUNC([setsockopt], [], [AC_CHECK_LIB([socket], [setsockopt])])

dnl Checks for header files.

AC_HEADER_STDC

AH_TEMPLATE([LOG4CPLUS_HAVE_SYS_TYPES_H])
AC_CHECK_HEADER([sys/types.h], [AC_DEFINE([LOG4CPLUS_HAVE_SYS_TYPES_H])])

AH_TEMPLATE([LOG4CPLUS_HAVE_UNISTD_H])
AC_CHECK_HEADER([unistd.h], [AC_DEFINE([LOG4CPLUS_HAVE_UNISTD_H])])

AH_TEMPLATE([LOG4CPLUS_HAVE_SYSLOG_H])
AC_CHECK_HEADER([syslog.h], [AC_DEFINE([LOG4CPLUS_HAVE_SYSLOG_H])])

AH_TEMPLATE([LOG4CPLUS_HAVE_NETINET_IN_H])
AC_CHECK_HEADER([netinet/in.h], [AC_DEFINE([LOG4CPLUS_HAVE_NETINET_IN_H])])

dnl Checks for library functions.

AH_TEMPLATE([LOG4CPLUS_HAVE_GMTIME_R])
AC_CHECK_FUNCS([gmtime_r], [AC_DEFINE([LOG4CPLUS_HAVE_GMTIME_R])])

AH_TEMPLATE([LOG4CPLUS_HAVE_LOCALTIME_R])
AC_CHECK_FUNCS([localtime_r], [AC_DEFINE([LOG4CPLUS_HAVE_LOCALTIME_R])])

AH_TEMPLATE([LOG4CPLUS_HAVE_GETTIMEOFDAY])
AC_CHECK_FUNCS([gettimeofday], [AC_DEFINE([LOG4CPLUS_HAVE_GETTIMEOFDAY])])

AH_TEMPLATE([LOG4CPLUS_HAVE_GETPID])
AC_CHECK_FUNCS([getpid], [AC_DEFINE([LOG4CPLUS_HAVE_GETPID])])

AH_TEMPLATE([LOG4CPLUS_HAVE_FTIME])
AC_CHECK_FUNCS([ftime], [AC_DEFINE([LOG4CPLUS_HAVE_FTIME])])

AH_TEMPLATE([LOG4CPLUS_HAVE_STAT])
AC_CHECK_FUNCS([stat], [AC_DEFINE([LOG4CPLUS_HAVE_STAT])])

AH_TEMPLATE([LOG4CPLUS_HAVE_LSTAT])
AC_CHECK_FUNCS([lstat], [AC_DEFINE([LOG4CPLUS_HAVE_LSTAT])])

AH_TEMPLATE([LOG4CPLUS_HAVE_HTONS])
AC_CHECK_FUNCS([htons], [AC_DEFINE([LOG4CPLUS_HAVE_HTONS])])

AH_TEMPLATE([LOG4CPLUS_HAVE_NTOHS])
AC_CHECK_FUNCS([ntohs], [AC_DEFINE([LOG4CPLUS_HAVE_NTOHS])])

AH_TEMPLATE([LOG4CPLUS_HAVE_HTONL])
AC_CHECK_FUNCS([htonl], [AC_DEFINE([LOG4CPLUS_HAVE_HTONL])])

AH_TEMPLATE([LOG4CPLUS_HAVE_NTOHL])
AC_CHECK_FUNCS([ntohl], [AC_DEFINE([LOG4CPLUS_HAVE_NTOHL])])

AH_TEMPLATE([LOG4CPLUS_HAVE_ENAMETOOLONG])
AC_COMPILE_IFELSE(
  [AC_LANG_PROGRAM([#include <errno.h>], [int value = ENAMETOOLONG;])],
  [AC_DEFINE([LOG4CPLUS_HAVE_ENAMETOOLONG])])

dnl Check for the existance of type socklen_t.
TYPE_SOCKLEN_T


dnl Check for single-threaded compilation

AH_TEMPLATE([LOG4CPLUS_USE_PTHREADS])
AC_ARG_ENABLE([threads],
  [AS_HELP_STRING([--enable-threads=yes/no],
    [Use this option to create a singled-threaded version of this library])],
  [AS_CASE(["${enableval}"],
     [yes],
     [multi_threaded=true],
     [no],
     [multi_threaded=false],
     [AC_MSG_ERROR([bad value ${enableval} for --enable-threads])])])

AS_IF([test x$multi_threaded = xtrue],
dnl Multi threaded library.
  [AC_MSG_NOTICE([Threads support:])
   CPPFLAGS="$CPPFLAGS -D_REENTRANT"
   ACX_PTHREAD([],
     [AC_MSG_ERROR([Requested threads support but no therads were found.])])
   AC_MSG_NOTICE([Creating a multi-threaded library.])
   CFLAGS="$CFLAGS $PTHREAD_CFLAGS"
   CXXFLAGS="$CXXFLAGS $PTHREAD_CFLAGS"
   LIBS="$PTHREAD_LIBS $LIBS"

   AS_CASE([$ac_cxx_compiler_vendor],
     [gnu],
     [dnl GCC, empty case to avoid false positive for -qtls check.
     ],
     [sun],
     [dnl Check for Sun's compiler's TLS option.
      AX_CXXFLAGS_GCC_OPTION([-xthreadvar])],
     [dnl Default case.
      dnl Check for IBM's compiler's TLS option.
      AX_CXXFLAGS_GCC_OPTION([-qtls])])

   AX__SYNC
   AH_TEMPLATE([LOG4CPLUS_HAVE___SYNC_ADD_AND_FETCH])
   AS_IF([test "x$ac_cv___sync_add_and_fetch" = "xyes"],
     [AC_DEFINE([LOG4CPLUS_HAVE___SYNC_ADD_AND_FETCH])])
   AH_TEMPLATE([LOG4CPLUS_HAVE___SYNC_SUB_AND_FETCH])
   AS_IF([test "x$ac_cv___sync_sub_and_fetch" = "xyes"],
     [AC_DEFINE([LOG4CPLUS_HAVE___SYNC_SUB_AND_FETCH])])

   AX_TLS_SUPPORT
   AH_TEMPLATE([LOG4CPLUS_HAVE_TLS_SUPPORT])
   AH_TEMPLATE([LOG4CPLUS_THREAD_LOCAL_VAR])
   AS_IF([test "x$ac_cv_declspec_thread" = "xyes"],
     [AC_DEFINE([LOG4CPLUS_HAVE_TLS_SUPPORT])
      AC_DEFINE([LOG4CPLUS_THREAD_LOCAL_VAR], [__declspec(thread)])],
     [AS_IF([test "x$ac_cv__thread_keyword" = "xyes"],
       [AC_DEFINE([LOG4CPLUS_HAVE_TLS_SUPPORT])
        AC_DEFINE([LOG4CPLUS_THREAD_LOCAL_VAR], [__thread])])])

   AC_CHECK_LIB([rt], [nanosleep])],
  
dnl Single threaded library.
  [AC_MSG_NOTICE([Creating a single-threaded library])
   AC_DEFINE([LOG4CPLUS_SINGLE_THREADED], [1],
     [Define if this is a single-threaded library.])])

AM_CONDITIONAL([MULTI_THREADED], [test x$multi_threaded = xtrue])

AH_TOP([#ifndef LOG4CPLUS_CONFIG_H])
AH_TOP([#define LOG4CPLUS_CONFIG_H])
AH_BOTTOM([#endif // LOG4CPLUS_CONFIG_H])

AC_CONFIG_HEADERS([include/log4cplus/config.h])
AC_CONFIG_HEADERS([include/log4cplus/config/defines.hxx])
AC_CONFIG_FILES([Makefile
           src/Makefile
           loggingserver/Makefile
           tests/Makefile
           tests/appender_test/Makefile
           tests/configandwatch_test/Makefile
           tests/customloglevel_test/Makefile
           tests/fileappender_test/Makefile
           tests/filter_test/Makefile
           tests/hierarchy_test/Makefile
           tests/loglog_test/Makefile
           tests/ndc_test/Makefile
           tests/ostream_test/Makefile
           tests/patternlayout_test/Makefile
           tests/performance_test/Makefile
           tests/priority_test/Makefile
	   tests/propertyconfig_test/Makefile
	   tests/socket_test/Makefile
           tests/thread_test/Makefile
           tests/timeformat_test/Makefile])

AC_SUBST([LOG4CPLUS_PROFILING_LDFLAGS])
AC_SUBST([LOG4CPLUS_PROFILING_CXXFLAGS])

AC_OUTPUT
